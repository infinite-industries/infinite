set dotenv-load

tag := "latest"
image := "infinite-industries/ops"

registry := "ghcr.io"
registry_user := "jswank"

# this environment variable will be passed to docker login as the password
registry_pass_var := "REGISTRY_PASSWORD"  

# build a new image
build:
  docker build -t {{image}}:{{tag}} -f Dockerfile ctx

_login:
  @ echo "${{ registry_pass_var }}" | docker login {{registry}} -u {{registry_user}} --password-stdin

_logout:
  @ docker logout {{registry}}

# publish the image
publish alt_tag=tag: _login
  @ docker tag {{image}}:{{tag}} {{registry}}/{{image}}:{{alt_tag}}
  @ docker push {{registry}}/{{image}}:{{alt_tag}}
  @ docker logout {{registry}}
