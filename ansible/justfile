flags := ""

# you're looking at it!
help:
  @just --list 

# Usage: `just init` or `just init prod`
# Do the initial software install & config for a host. 
init env="staging":
  ansible-playbook -l {{ env }} {{ flags }} base_playbook.yml 


# Usage: `just deploy` or `just deploy prod`
# 
# The flags variable can be used to pass arguments to ansible-playbook.  For
# instance, to override the inventory variable image_version, one might run:
#   just deploy prod flags='-e image_version=development'
#
# Deploy the site.
deploy env="staging":
  ansible-playbook -l {{ env }} {{ flags }} deploy_site_playbook.yml

# cache the passphrase used to decrypt files
cache-pass:
  #!/bin/sh
  printf "Ansible Vault Passphrase: "
  trap 'stty echo' INT EXIT
  stty -echo
  read pass
  printf '\n'
  printf '%s\n' "${pass}" > .password
  chmod 600 .password
